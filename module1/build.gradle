apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "27.0.0"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //https://developer.android.com/studio/build/build-variants.html#sourcesets
    sourceSets {
        navigation {
            setRoot 'src/navigation'
        }
        main { java.srcDirs = ['src/main/java', 'src/navigation/java'] }
    }

    configurations {
        intentBuilder
        navigationApi {
            canBeResolved true
        }
    }

    libraryVariants.all { variant ->
        def compileTask = variant.javaCompiler
        def intentBuilderCompileTask = task "intentBuilderCompile${variant.name.capitalize()}"(type: JavaCompile) {
            setSource(android.sourceSets.navigation.java.sourceFiles)
            setDestinationDir(compileTask.getDestinationDir())
            classpath = configurations.navigationApi
            options.compilerArgs = ["-proc:only"] + compileTask.options.compilerArgs
            options.annotationProcessorPath = compileTask.options.annotationProcessorPath
            setTargetCompatibility(compileTask.getTargetCompatibility())
            setSourceCompatibility(compileTask.getSourceCompatibility())
            doFirst {
                file("${buildDir}/generated/source/apt/debug/").mkdirs()
            }
        }


        def intentBuilderJarTask = task "intentBuilderJar${variant.name.capitalize()}"(type: Jar, dependsOn: intentBuilderCompileTask) {
            baseName = "module1-${variant.name.capitalize()}-intentBuilder"
            from compileTask.destinationDir
            include('**/**IntentBuilder**')
        }

        artifacts {
            intentBuilder intentBuilderJarTask
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    //implementation project(path: ':module1', configuration: 'intentBuilder')
    navigationApi "com.f2prateek.dart:dart-annotations:2.0.3"
    navigationApi 'com.android.support:appcompat-v7:26.1.0'
    navigationApi "com.f2prateek.dart:dart:2.0.3"
    navigationApi "com.f2prateek.dart:henson:2.0.3"
    navigationApi "com.google.android:android:4.1.1.4"

    implementation "com.f2prateek.dart:dart:2.0.3"
    implementation "com.f2prateek.dart:henson:2.0.3"
    annotationProcessor "com.f2prateek.dart:henson-processor:2.0.3"
    annotationProcessor "com.f2prateek.dart:dart-processor:2.0.3"

    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
